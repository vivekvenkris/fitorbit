C note that this file causes problems with the optimizer on the SUN PAH -910902

*DECK CPARAM
C
C ***********************************************************
      CHARACTER*(*) FUNCTION CPARAM ( PARAM, PARDSC, PARID,
     &     IPAR, RPAR, DPAR, LPAR, CPAR,
     &     IIUNITS, IRUNITS, IDUNITS )
C ***********************************************************
C
C FORMS A SINGLE LINE FOR OUTPUT FOR
C     THE PARAMETER WHOSE NAME, DESCRIPTION AND ID ARE GIVEN
C     IN PARAM, PARDSC AND PARID.
C IPAR   THROUGH CPAR ARE THE PARAMETER ARRAYS.
C IIUNITS, IRUNITS AND IDUNITS ARE THE UNIT SPECIFICATION ARRAYS.
C
      CHARACTER*(*) PARAM,PARDSC,PARID,CPAR(*)
      DIMENSION IPAR(*),RPAR(*),IIUNITS(*),IRUNITS(*)
     &         ,IDUNITS(*)
      DOUBLE PRECISION SUNIT,DPAR(*),SCALE
      LOGICAL LPAR(*)
C
C     DECLARE EXTERNAL CHARACTER FUNCTIONS.
C
      CHARACTER CUNIT*20,CRJUST*40,CPOSTN*25,CJTIME*30,CSWITCH*3
C
C     INTERNAL BUFFERS.
C
      CHARACTER FMT*20,BUF*40,CUN*20
C
C     IF THE PARAMETER IS NOT BEING USED, RETURN.
C
      IF ( PARID(1:1).EQ.'*' ) RETURN
C
C     PARAMETER DESCRIPTION AT THE START.
C
      CPARAM = ' '//PARDSC
C
C     ADD THE NAME OF THE PARAMETER IF PRESENT.
C     IC IS THE STARTING POINT FOR THE PARAMETER CONTENTS.
C
      IF ( PARAM.EQ.' ' ) THEN
         IC = 32
      ELSE
         CPARAM(32:) = PARAM
         IC = 42
      ENDIF
C
C     DECODE THE PARAMETER IDENTIFICATION.
C     IPOSN IS THE POSITION OF THE PARAMETER IN THE APPROPRIATE ARRAY.
C
      READ (PARID,998) IPOSN
C
C     SET THE SCALING FACTOR AND THE NAME OF THE UNIT
C     FROM THE UNIT ARRAYS.
C
      IF ( PARID(1:1).EQ.'I'.OR.PARID(1:1).EQ.'L' ) THEN
         SCALE = SUNIT(IIUNITS(IPOSN))
         CUN   = CUNIT(IIUNITS(IPOSN))
      ELSEIF ( PARID(1:1).EQ.'R'.OR.PARID(1:1).EQ.'S' ) THEN
         SCALE = SUNIT(IRUNITS(IPOSN))
         CUN   = CUNIT(IRUNITS(IPOSN))
      ELSEIF ( PARID(1:1).EQ.'D' ) THEN
         SCALE = SUNIT(IDUNITS(IPOSN))
         CUN   = CUNIT(IDUNITS(IPOSN))
      ENDIF
C
C     WRITE THE PARAMETER TO THE FUNCTION NAME IN THE APPROPRIATE FORM.
C
C     INTEGER.
C
      IF ( PARID(1:1).EQ.'I' ) THEN
         WRITE (CPARAM(IC:IC+29),997) INT(IPAR(IPOSN)/SCALE),CUN
C
C     REAL.
C
      ELSEIF ( PARID(1:1).EQ.'R' ) THEN
         WRITE (CPARAM(IC:IC+29),996) RPAR(IPOSN)/SCALE,CUN
C
C     DOUBLE PRECISION.
C
      ELSEIF ( PARID(1:1).EQ.'D' ) THEN
         WRITE (CPARAM(IC:IC+29),988) DPAR(IPOSN)/SCALE,CUN
C
C     POSITION.
C
      ELSEIF ( PARID(1:1).EQ.'P' ) THEN
         BUF = CRJUST(CPOSTN(DPAR(IPOSN),3,0))
         CPARAM(IC:IC+29) = BUF(11:)
C
C     DATE/TIME.
C
      ELSEIF ( PARID(1:1).EQ.'T' ) THEN
         BUF = CRJUST(CJTIME(DPAR(IPOSN),6))
         CPARAM(IC:IC+29) = BUF(11:)
C
C     LIST OF INTEGERS.
C
      ELSEIF ( PARID(1:1).EQ.'L' ) THEN
         IF ( IPAR(IPOSN+1).EQ.0 ) THEN
            WRITE (CPARAM(IC:IC+29),995)
         ELSE
            DO 10 I=1,IPAR(IPOSN+1)
               IF ( LEN(CPARAM)-LENGTH(CPARAM).GT.15 ) THEN
                  FMT = '(1X,I?)'
                  IVAL = INT(IPAR(IPOSN+I+1)/SCALE)
                  CALL SETIFM(IVAL,FMT)
                  WRITE (CPARAM(MAX(IC,LENGTH(CPARAM)+1):),FMT) IVAL
               ELSE
                  CPARAM = CPARAM(1:LENGTH(CPARAM))//' etc.'
                  GOTO 20
               ENDIF
   10       CONTINUE
   20       CONTINUE
         ENDIF
         CPARAM(LEN(CPARAM)-9:) = CUN
C
C     LIST OF REALS.
C
      ELSEIF ( PARID(1:1).EQ.'S' ) THEN
         IF ( RPAR(IPOSN+1).EQ.0.0 ) THEN
            WRITE (CPARAM(IC:IC+29),995)
         ELSE
            DO 30 I=1,RPAR(IPOSN+1)
               IF ( LEN(CPARAM)-LENGTH(CPARAM).GT.10 ) THEN
                  FMT = '(F??.1)'
                  VAL = RPAR(IPOSN+I+1)/SCALE
                  CALL SETRFM(VAL,FMT)
                  WRITE (CPARAM(MAX(IC,LENGTH(CPARAM)+2):),FMT) VAL
               ELSE
                  CPARAM = CPARAM(1:LENGTH(CPARAM))//' etc.'
                  GOTO 40
               ENDIF
   30       CONTINUE
   40       CONTINUE
         ENDIF
         CPARAM(LEN(CPARAM)-9:) = CUN
C
C     CHARACTER.
C
      ELSEIF ( PARID(1:1).EQ.'C' ) THEN
         BUF = CRJUST(CPAR(IPOSN))
         CPARAM(IC:IC+29) = BUF(11:)
C
C     FILENAME.
C
      ELSEIF ( PARID(1:1).EQ.'F' ) THEN
         IF ( PARAM.EQ.' ' ) THEN
            WRITE (CPARAM(IC-10:IC+29),989) CRJUST(CPAR(IPOSN))
         ELSE
            WRITE (CPARAM(IC-20:IC+29),993) PARAM,CRJUST(CPAR(IPOSN))
         ENDIF
C
C     SWITCH.
C
      ELSEIF ( PARID(1:1).EQ.'W' ) THEN
         CPARAM(IC+27:IC+29) = CSWITCH(LPAR(IPOSN))
C
C     LIST OF OPTIONS.
C
      ELSEIF ( PARID(1:1).EQ.'O' ) THEN
         IF ( IPAR(IPOSN).GT.0 ) THEN
            CPARAM(IC:IC+29) = CPAR(IPAR(IPOSN)+IPAR(IPOSN+1)-1)
         ELSE
            WRITE (CPARAM(IC:IC+29),995)
         ENDIF
      ENDIF
C
      RETURN
C
  998 FORMAT ( 1X,I2 )
  997 FORMAT ( I20,1X,A9 )
  996 FORMAT ( F20.8,1X,A9 )
  995 FORMAT ( 12X,'<none>',12X )
  993 FORMAT ( A10,A40 )
  989 FORMAT ( A40 )
  988 FORMAT ( F20.12,1X,A9 )
C
C END OF CHARACTER FUNCTION CPARAM.
C
      END
