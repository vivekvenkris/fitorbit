*DECK TSTDAT
C
C **********************************************************************
      SUBROUTINE TSTDAT ( A, IS, N, START, W, P, HT, OFFSET, CLIP, RMS )
C **********************************************************************
C
C FORMS A PULSE TRAIN OF LENGTH N, AND STRIDE IS, IN THE BYTE ARRAY A.
C     THE PULSES HAVE PERIOD P, WIDTH W, STARTING AT START,
C     ( ALL IN BINS ) SUPERIMPOSED ON GAUSSIAN NOISE WITH A SIGMA OF RMS
C     AND A MEAN LEVEL OF OFFSET.
C     THE DATA IS CLIPPED AT CLIP.
C
      BYTE A(*),SRTMAX
      DATA ISEED/123/
C
C     CLIPPING LEVEL IS THE SMALLER OF CLIP AND MAXIMUM ALLOWED IN A
C     SHORT VARIABLE.
C
      RSRTMAX = SRTMAX()
      ICLIP = MIN(CLIP,RSRTMAX)
C
C     FILL THE DATA ARRAY WITH GAUSSIAN NOISE TRUNCATED AT ZERO
C     AND ADD SQUARE PULSES OF HEIGHT HT AND WIDTH W STARTING AT BIN
C     START OF EACH PERIOD.
C
      DO 10 I=1,N
C
C        OBTAIN A NOISE ELEMENT.
C
         DD = GRAN(OFFSET,RMS,ISEED)
C
C        CALCULATE THE PULSE PHASE, IN RANGE 0 TO P.
C
         PH = MOD(FLOAT(I)-START,P)
         IF ( PH.LT.0.0 ) PH = PH+P
C
C        TEST WHETHER THE PHASE LIES WITHIN THE PULSE WINDOW, W.
C
         IF ( PH.GE.0.0.AND.PH.LE.W+1.0 ) THEN
C
C           CALCULATE THE HEIGHT AT THIS PHASE OF THE PULSE.
C
            F = 1.0
            IF ( PH.LT.1.0 ) F = PH
            IF ( PH.GT.W   ) F = W+1.0-PH
C
C           ADD THE PULSE TO THE NOISE.
C
            DD = DD+F*HT
         ENDIF
C
C        LOAD THE DATA ELEMENT INTO THE ARRAY, CLIPPING AT ICLIP.
C
         A((I-1)*IS+1) = MIN(NINT(MAX(0.0,DD)),ICLIP)
   10 CONTINUE
C
      RETURN
C
C END OF SUBROUTINE TSTDAT.
C
      END
