*DECK SHWCMD
C
C **************************************************************
      SUBROUTINE SHWCMD ( CMDINP, NINP, JINP ,PROGID
     &    ,GBLLST ,GBLDSC ,CMDLST ,CMDDSC
     &    ,PARLST ,PARDSC ,PARID
     &    ,IPAR ,RPAR ,DPAR ,LPAR ,CPAR
     &    ,IIUNITS ,IRUNITS ,IDUNITS ,IFAIL )
C **************************************************************
C
C HANDLES THE SHOW COMMAND.
C PROGID CONTAINS THE PROGRAM NAME.
C CMDLST CONTAINS THE LISTS OF COMMANDS.
C CMDDSC CONTAINS THE COMMAND DESCRIPTIONS WITH TWO DESCRIPTION
C     LINES FOR EACH COMMAND.
C GBLLST AND GBLDSC CONTAIN THE GLOBAL COMMANDS AND THEIR DESCRIPTIONS.
C CMDINP CONTAIN THE ACTUAL COMMANDS GIVEN BY THE USER, WHERE NINP
C     ARE THEIR NUMBER AND JINP POINTS TO THE NEXT COMMAND TO BE
C     INTERPRETED.
C PARLST CONTAINS THE LIST OF PARAMETERS AND PARDSC THEIR DESCRIPTIONS,
C     WITH TWO DESCRIPTION LINES PER PARAMETER.
C PARID CONTAINS THE LISTS OF PARAMETER IDENTIFIERS AS REQUIRED BY THE
C     PARAMETER HANDLING ROUTINES AND MAXPAR IS THE MAXIMUM NUMBER OF
C     PARAMETERS. THERE MUST BE A PARAMETER IDENTIFIER FOR EACH 
C     PARAMETER IN THE LIST.
C IPAR,RPAR,LPAR,DPAR,CPAR,IIUNITS,IRUNITS,IDUNITS
C     ARE THE PARAMETER LISTS AND THEIR UNITS. THEY ARE DESCIBED IN THE
C     PARAMETER HANDLING ROUTINES.
C IFAIL IS ZERO UNLESS AN ERROR OCCURRED.
C 
      CHARACTER*(*) PROGID,CMDLST(*),CMDDSC(*),GBLLST(*),GBLDSC(*),
     &   CMDINP(*),PARLST(*),PARDSC(*),CPAR(*),PARID(*)
      INTEGER IPAR(*),IIUNITS(*),IRUNITS(*),IDUNITS(*)
      REAL RPAR(*)
      DOUBLE PRECISION DPAR(*)
      LOGICAL LPAR(*)
C
      CHARACTER*10 CMDS(5),OUTBUF*81
      data cmds / 'commands','parameters','options'
     &           ,'units',' ' /
C
C     CLEAR THE ERROR FLAG.
C
      IFAIL = 0
C
C     CHECK FOR NO INPUT COMMANDS.
C
      IF ( JINP.GT.NINP ) THEN
         IFAIL = 23
         CALL PSRERR ('SHWCMD',IFAIL,0,0.0,' ')
         RETURN
      ENDIF
C
C     CHECK INPUT COMMAND AGAINST LIST.
C
      CALL SPARSE (CMDINP,NINP,JINP,CMDS,ICMD,OUTBUF,IFAIL)
      IF (IFAIL.NE.0) THEN
         IF (IFAIL.EQ.26) IFAIL=5
         RETURN
      ENDIF
C
C     INCREMENT THE COMMAND POINTER.
C
      JINP = JINP+1
C
C     PERFORM THE REQUIRED COMMAND.
C
C     SHOW COMMAND LISTS.
C
      IF ( ICMD.EQ.1 ) THEN
         CALL SHOCMD (CMDLST,CMDDSC,GBLLST,GBLDSC)
C
C     SHOW PARAMETERS.
C
      ELSEIF ( ICMD.EQ.2 ) THEN
         CALL SHOPAR ( CMDINP,NINP,JINP,PROGID
     &       ,PARLST,PARDSC,PARID
     &       ,IPAR,RPAR,DPAR,LPAR,CPAR
     &       ,IIUNITS,IRUNITS,IDUNITS,IFAIL )
C
C     SHOW PARAMETER OPTIONS.
C
      ELSEIF ( ICMD.EQ.3 ) THEN
         CALL SHOOPT (CMDINP,NINP,JINP,PARLST,PARID
     &       ,IPAR,RPAR,DPAR,LPAR,CPAR
     &       ,IIUNITS,IRUNITS,IDUNITS,IFAIL )
C
C     SHOW LIST OF UNITS.
C
      ELSEIF ( ICMD.EQ.4 ) THEN
         CALL SHOUNT ( )
      ENDIF
C
      RETURN
C
C END OF SUBROUTINE SHWCMD.
C
      END
