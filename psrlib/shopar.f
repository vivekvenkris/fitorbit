*DECK SHOPAR
C
C **********************************************************************
      SUBROUTINE SHOPAR ( CMDINP , NINP, JINP,
     &     IDENT, PARLST, PARDSC, PARID,
     &     IPAR, RPAR, DPAR, LPAR, CPAR,
     &     IIUNITS, IRUNITS, IDUNITS, IFAIL )
C **********************************************************************
C
C HANDLES THE SHOW PARAMETERS COMMAND.
C CMDINP CONTAIN THE COMMANDS GIVEN BY THE USER, WHERE NINP
C     ARE THEIR NUMBER AND JINP POINTS TO THE NEXT COMMAND TO BE
C     INTERPRETED.
C IDENT CONTAINS THE NAME OF THE PARAMETER LIST.
C PARLST CONTAINS THE LIST OF PARAMETERS AND PARDSC THEIR DESCRIPTIONS,
C     WITH TWO DESCRIPTION LINES PER PARAMETER.
C PARID CONTAINS THE LISTS OF PARAMETER IDENTIFIERS AS REQUIRED BY THE
C     PARAMETER HANDLING ROUTINES.THERE MUST BE A PARAMETER IDENTIFIER
C     FOR EACH PARAMETER IN THE LIST.
C IPAR,RPAR,LPAR,DPAR,CPAR,IIUNITS,IRUNITS,IDUNITS
C     ARE THE PARAMETER LISTS AND THEIR UNITS. THEIR USE IS DESCRIBED
C     IN THE PARAMETER HANDLING ROUTINES.
C IFAIL IS ZERO UNLESS AN ERROR OCCURRED.
C
      CHARACTER*(*) CMDINP(*),IDENT,PARLST(*),PARDSC(*),PARID(*),CPAR(*)
      DIMENSION IPAR(*),RPAR(*),IIUNITS(*),IRUNITS(*)
     &         ,IDUNITS(*)
      DOUBLE PRECISION DPAR(*)
      LOGICAL LPAR(*),COMSTR,LMANY
C
C     DECLARE EXTERNAL CHARACTER FUNCTIONS
C
      CHARACTER CPARAM*151
C
C     INTERNAL BUFFERS
C
      CHARACTER OUTBUF*151,BUF*25
C
C     CLEAR ERROR FLAG.
C
      IFAIL = 0
      LMANY=.FALSE.
C
C     IF FURTHER COMMANDS HAVE BEEN SPECIFIED, LOOK FOR 'FROM' PARAMETER.
C
      IF ( JINP.LE.NINP ) THEN
         IF ( COMSTR('FROM',CMDINP(JINP)) ) THEN
           JINP = JINP+1
           LMANY=.TRUE.
         ENDIF
C
C        CHECK FOR PARAMETER NAME.
C
         IF ( JINP.GT.NINP ) THEN
            IFAIL = 23
            CALL PSRERR ('SHOPAR',IFAIL,0,0.0,' ')
            RETURN
         ENDIF
C
C        LOOK FOR PARAMETER NAME IN PARAMETER LIST.
C
         CALL FNDPAR (CMDINP(JINP),PARLST,PARID,IP,IFAIL)
         IF ( IFAIL.NE.0 ) RETURN
         JINP = JINP+1
      ELSE
C
C        NO 'FROM' WAS SPECIFIED, START AT THE BEGINNING.
C
         IP = 1
         LMANY=.TRUE.
      ENDIF
C
C     CHECK PARAMETER LIST IS NOT EMPTY.
C
      IF ( PARLST(1).EQ.' ' ) THEN
         IFAIL = 42
         CALL PSRERR ('SHOPAR',IFAIL,0,0.0,' ')
         RETURN
      ENDIF
C
C     OUTPUT THE PARAMETER HEADING
C
      IF(LMANY) THEN
        L = LENGTH(IDENT)
        OUTBUF = ' '//IDENT(1:LENGTH(IDENT))
        OUTBUF(L+2:) = ' parameters:-'
        CALL OUTMON ( OUTBUF )
        CALL OUTMON ( ' ' )
      ENDIF
      I = IP-1
   10 CONTINUE
         I = I+1
         IF ( PARDSC(I*2-1).EQ.' ' ) GOTO 20
         IF ( PARID(I)(1:1).EQ.'*' ) THEN
           IF(.NOT.LMANY) CALL OUTMON(' Parameter not in use')
           GOTO 10
         ENDIF
         OUTBUF = CPARAM (PARLST(I),PARDSC(I*2-1),PARID(I)
     &               ,IPAR,RPAR,DPAR,LPAR,CPAR,IIUNITS,IRUNITS,IDUNITS )
         CALL OUTMON ( OUTBUF )
         IF ( PARDSC(I*2).NE.' ' ) THEN
            CALL OUTMON ( ' '//PARDSC(I*2)(1:LENGTH(PARDSC(I*2))) )
         ENDIF
      IF(LMANY) GOTO 10
   20 CONTINUE
      CALL OUTMON ( ' ' )
C
      RETURN
C
C END OF SUBROUTINE SHOPAR
C
      END
