*DECK INTUNT
C
C ********************************************************************
      FUNCTION INTUNT ( ULIST, STRING )
C ********************************************************************
C
C COMPARES THE CHARACTERS IN STRING AGAINST THE LIST IN ULIST AND
C     RETURNS THE POSTION IN THE LIST WHICH AGREES WITH STRING.
C THE STRINGS MAY CONSIST OF WORDS SEPARATED BY *, ** OR /.
C TWO STRINGS AGREE IF THEY HAVE THE SAME NUMBER OF WORDS AND
C     EACH WORD AGREES UP TO AS MANY CHARACTERS AS ARE CONTAINED
C     IN THAT WORD IN STRING.
C     E.G. M/S AGREES WITH METRES/SECOND
C     BUT IS DISTINCT FROM METRES/SECOND**2
C IF NO MATCH CAN BE FOUND THEN INTUNT IS ZERO ON EXIT.
C IF AN AMBIGUITY IS DETECTED THE POSITION OF THE FIRST MATCH FOUND
C     IS RETURNED, BUT NEGATED.
C ULIST MUST BE TERMINATED WITH A BLANK STRING.
C
      CHARACTER ULIST(*)*(*),STRING*(*)
      LOGICAL COMSTR
C
C SET INTUNT.
C
      INTUNT=0
C
C SCAN THROUGH ULIST COMPARING THE ENTRIES WITH STRING UNTIL THE END OF
C THE LIST IS REACHED.
C
      I=1
 1000 IF(ULIST(I).NE.' ') THEN
C
C COMPARE THE ENTRY AGAINST STRING.
C
        IF(COMSTR(ULIST(I),STRING)) THEN
C
C THEY AGREE, TEST IF A PREVIOUS ENTRY ALSO AGREED.
C
          IF(INTUNT.NE.0) THEN
C
C YES, NEGATE POINTER AND EXIT.
C
             INTUNT=-INTUNT
             RETURN
          ENDIF
C
C SET INTUNT TO THE LIST ENTRY NUMBER.
C
          INTUNT=I
        ENDIF
C
C GET THE NEXT ENTRY.
C
        I=I+1
        GOTO 1000
      ENDIF
C
C END OF LIST REACHED, EXIT.
C
      RETURN
C
C END OF INTEGER FUNCTION INTUNT.
C
      END
