*DECK TSTPLS
C
C **********************************************************************
      SUBROUTINE TSTPLS ( A, IS, N, START, W, P, HT )
C **********************************************************************
C
C ADDS A PULSE TRAIN INTO N ELEMENTS AT STRIDE IS OF THE BYTE ARRAY A.
C     THE PULSE TRAIN HAS HEIGHT HT, PERIOD P, WIDTH W, STARTING AT 
C     START.
C
C THIS ROUTINE IS INSTALLATION DEPENDENT.
C
C VAX-11 FORTRAN VERSION.
C
      BYTE A(*),SRTMAX
      DATA ISEED /123/
C
C     MAKE AN INTEGER VERSION OF THE MAXIMUM BYTE NUMBER.
C
      ISRTMAX = SRTMAX()
C
C     ADD SQUARE PULSES OF HEIGHT HT AND WIDTH W STARTING AT BIN
C     START OF EACH PERIOD.
C
      DO 10 I=1,N
C
C        CALCULATE THE PULSE PHASE.
C
         PH = MOD(FLOAT(I),P)-START
C
C        TEST WHETHER THE PHASE IS WITHIN THE PULSE WINDOW, W.
C
         IF ( PH.GE.-1.0.AND.PH.LT.W ) THEN
C
C           CALCULATE THE HEIGHT OF THE PULSE AT THIS PHASE.
C
            F = 1.0
            IF ( PH.LT.0.0 )   F = 1.0-ABS(PH)
            IF ( PH.GT.W-1.0 ) F = W-PH
C
C           SPLIT THE HEIGHT INTO INTEGER AND FRACTIONAL PARTS.C

            IHT = INT(HT*F)
            FHT = HT*F - IHT
C
C           MAKE A RANDOM DECISION ON WHETHER TO ROUND UP OR DOWN.
C
            IF (FHT.GT.RAN(ISEED)) IHT = IHT + 1
C
C           ADD THE PULSE INTO THE DATA ARRAY, ENSURING IT DOES NOT 
C           OVERFLOW THE BYTE VARIABLES.
C
            A((I-1)*IS+1) = MIN(A((I-1)*IS+1)+IHT,ISRTMAX)
         ENDIF
   10 CONTINUE
C
      RETURN
C
C END OF SUBROUTINE TSTPLS.
C
      END
