*DECK CPSRID
C
C ************************************************************
      CHARACTER*8 FUNCTION CPSRID ( RA, DEC )
C ************************************************************
C
C FORMS THE PULSAR NAME FROM THE RA AND DEC ( DEGREES )
C IN THE FORM HHMMsDDd
C WHERE HH ARE THE HOURS OF RA
C       MM ARE THE MINUTES OF RA
C       s  IS THE SIGN OF DEC
C       DD ARE THE DEGREES OF DEC
C       d  IS THE TENTHS OF A DEGREE OF DEC WHERE A CONFLICT OF
C             NAMES BETWEEN TWO SOURCES WOULD OTHERWISE RESULT
C
      PARAMETER ( NCONFL=3 )
      CHARACTER*7 CONFL(NCONFL)
      DATA CONFL / '1647-52','1913+16','1925+18' /
C
      WRITE (CPSRID(1:2),100) INT(RA/15)
      WRITE (CPSRID(3:4),100) INT((RA-INT(RA/15)*15)*4)
      IF ( DEC.LT.0.0 ) THEN
          CPSRID(5:5) = '-'
      ELSE
          CPSRID(5:5) = '+'
      ENDIF
      WRITE (CPSRID(6:7),100) INT(ABS(DEC))
C
C SEARCH THE CONFLICT TABLE
C IF THE PSRID IS PRESENT ADD THE TENTHS OF DEC
C
      DO 10 I=1,NCONFL
         IF ( CPSRID(1:7).EQ.CONFL(I) ) THEN
            WRITE (CPSRID(8:8),110) INT(AMOD(DEC,1.0)*10)
            RETURN
         ENDIF
   10 CONTINUE
      CPSRID(8:8) = ' '
C
      RETURN
C
C FORMATS
C
  100 FORMAT ( I2.2 )
  110 FORMAT ( I1.1 )
C
C END OF CHARACTER FUNCTION CPSRID
C
      END
