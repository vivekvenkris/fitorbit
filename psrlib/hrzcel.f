*DECK HRZCEL
C
C
C *****************************************************************
        SUBROUTINE HRZCEL(AZDEG,ELDEG,LSTSEC,RADEG,DECDEG)
C *****************************************************************
C
C      THIS ROUTINE CONVERTS HORIZON COORDINATES TO EQUATORIAL
C  WITH DOUBLE PRECISION ACCURACY.
C  PARAMETERS :-
C   INPUT:
C          AZDEG - AZIMUTH IN DECIMAL DEGREES.
C          ELDEG - ELEVATION IN DECIMAL DEGREES.
C          LSTSEC - LOCAL SIDEREAL TIME IN DECIMAL SECONDS.
C   OUTPUT:
C          RADEG - RIGHT ASCENSION IN DECIMAL DEGREES.
C          DECDEG - DECLINATION IN DECIMAL DEGREES.
C
C N.B. ALL PARAMETERS ARE SINGLE PRECISION REAL NUMBERS. ALL INPUT
C      PARAMETERS ARE UNCHANGED ON EXIT.
C
*******************************************************************
C
         DOUBLE PRECISION LST,AZ,EL,COSCOS,COSSIN,LAT,HA
         DOUBLE PRECISION PI,TWOPI
         REAL AZDEG,ELDEG,LSTSEC,RADEG,DECDEG
C
         PARAMETER( PI=3.141592654D0 , TWOPI=2.0D0*PI )
         PARAMETER( LAT=53.236666667D0*TWOPI/360.0D0 )
C
C CHANGE INPUT COORDINATES TO DOUBLE PRECISION RADIANS
C
         AZ=DBLE(AZDEG)*TWOPI/360.0D0
         EL=DBLE(ELDEG)*TWOPI/360.0D0
C
C CONVERT LOCAL SIDEREAL TIME TO RADIANS
C
         LST=DBLE(LSTSEC)*TWOPI/86400.0D0
C
C NOW CALCULATE THE HOUR ANGLE (HA)
C
         COSCOS=DCOS(LAT)*DSIN(EL)-
     +                      DSIN(LAT)*DCOS(EL)*DCOS(AZ)
         COSSIN=-DCOS(EL)*DSIN(AZ)
         HA=DATAN2(COSSIN,COSCOS)
C
C FIND RIGHT ASCENSION..
C
         RADEG=REAL((LST-HA)*360.0D0/TWOPI)
         IF(RADEG.LT.0.0)RADEG=RADEG+360.0
C
C CALCULATE DECLINATION ..
C
         DECDEG=REAL(360.0/TWOPI*DASIN(DSIN(LAT)*DSIN(EL)+
     +                DCOS(LAT)*DCOS(EL)*DCOS(AZ)))
C
         RETURN
C
C  END OF SUBROUTINE HRZCEL
C
         END
