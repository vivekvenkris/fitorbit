*DECK CPOSTN
C
C
C
      CHARACTER*(*) FUNCTION CPOSTN(POS1,NDEC,ISIGN)
C
C FORM A CHARACTER STRING FOR A POSITION POS1 IN SECONDS WITH NDEC
C DECIMAL PLACES. IF ISIGN.EQ.0 SUPRESS + SIGN.
C
      CHARACTER FMT*13
      DOUBLE PRECISION POS,POS1,DROUND,X
C
C ROUND THE POSITION TO THE REQUESTED NUMBER OF DECIMAL PLACES.
C
      ND=MIN(27,MAX(0,NDEC))
      POS=DROUND(POS1,ND)
C
C DEAL WITH SIGN.
C
      CPOSTN=' '
      IF(POS.LT.0.0) THEN
        CPOSTN(1:1)='-'
        IC=1
      ELSEIF(ISIGN.NE.0) THEN
        CPOSTN(1:1)='+'
        IC=1
      ELSE
        IC=0
      ENDIF
C
C DELIMITERS.
C
      CPOSTN(3+IC:3+IC)=':'
      CPOSTN(6+IC:6+IC)=':'
C
C HOURS OR DEGREES.
C
      X=ABS(POS)
      I=X/3600.0
      WRITE(CPOSTN(1+IC:2+IC),100) I
C
C MINUTES.
C
      X=X-3600.0*I
      I=X/60.0
      WRITE(CPOSTN(4+IC:5+IC),100) I
C
C SECONDS WITHOUT FRACTIONAL PART.
C
      X=X-60.0*I
      IF(ND.LE.0) THEN
        I=NINT(X)
        WRITE(CPOSTN(7+IC:8+IC),100) I
C
C SECONDS WITH FRACTIONAL PART.
C
      ELSE
        WRITE(FMT,101) ND+3,NDEC
        WRITE(CPOSTN(7+IC:ND+9+IC),FMT) X
C
C REPLACE SPACES WITH ZEROS.
C
        DO 1 I=7+IC,8+IC
          IF(CPOSTN(I:I).EQ.' ') CPOSTN(I:I)='0'
    1   CONTINUE
      ENDIF
      RETURN
C
C FORMATS.
C
  100 FORMAT(SS,I2.2)
  101 FORMAT(SS,'(SS,F',I3,'.',I3,')')
C
C END OF CHARACTER*(*) FUNCTION CPOSTN.
C
      END
