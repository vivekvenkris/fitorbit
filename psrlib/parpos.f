*DECK PARPOS
C
C
C
      LOGICAL FUNCTION PARPOS(PAR,POS)
C
C ATTEMPTS TO INTERPRET THE PARAMETER STRING PAR AS A POSITION.  RETURNS
C THE VALUE .TRUE. IF SUCCESSFUL AND .FALSE. OTHERWISE.  ON EXIT POS
C CONTAINS THE POSITION IN SECONDS IF PARPOS IS .TRUE. AND IS UNCHANGED
C OTHERWISE.  PARPOS DOES NOT CHECK THE POSITION FOR RANGE IF IT IS
C A RIGHT ASCENSION BUT THE DECLINATION LIMITS ARE CHECKED.  THE
C RA LIMITS MUST BE TESTED SEPARATELY IN THE CALLING ROUTINE.
C     VERSION 1.1   19MAY83
C
      CHARACTER PAR*(*),STR(2)*2,DIGITS*12
      LOGICAL PARDBL
      DOUBLE PRECISION POS,SECS
      DATA DIGITS /'+-0123456789'/
C
C SET UP THE INITIAL VALUES.
C
      PARPOS=.FALSE.
      DO 1 I=1,2
    1 STR(I)=' '
      J=1
      IC=0
C
C SCAN THROUGH PAR TO LOCATE THE FIRST NON-SPACE CHARACTER.
C
      LP=LENGTH(PAR)
      DO 2 I=1,LP
        IF(PAR(I:I).NE.' ') GOTO 1000
    2 CONTINUE
      RETURN
C
C CHECK IF THE FIRST CHARACTER IS A SIGN.
C
 1000 IF(PAR(I:I).EQ.'-') THEN
        SGN=-1.0
        I=I+1
      ELSEIF(PAR(I:I).EQ.'+') THEN
        SGN=1.0
        I=I+1
      ELSE
        SGN=1.0
      ENDIF
C
C NOW SCAN THROUGH THE REMAINING CHARACTERS AND LOAD HM OR DM INTO STR.
C
      DO 3 K=I,LP
        IF(INDEX(DIGITS,PAR(K:K)).EQ.0) THEN
C
C SEPARATOR ENCOUNTERED, INCREMENT THE DMS COUNTER.
C
          J=J+1
          IF(J.GT.2) THEN
C
C START OF THE SECONDS PART.
C
            GOTO 1001
          ENDIF
C
C CLEAR THE CHARACTER COUNTER.
C
          IC=0
        ELSEIF(PAR(K:K).NE.' ') THEN
C
C CHARACTER OTHER THAN A SPACE, LOAD IT INTO STR.
C
          IC=IC+1
          IF(IC.GT.2) THEN
C
C STRING IS TOO LONG, NOT A POSITION.
C
            RETURN
          ENDIF
C
C LOAD INTO STR.
C
          STR(J)(IC:IC)=PAR(K:K)
        ENDIF
    3 CONTINUE
C
C NOW ATTEMPT TO READ THE SECONDS PART AS A REAL NUMBER.
C
 1001 IF(K.GE.LP) THEN
C
C NO SECONDS SUPPLIED.
C
        SECS=0.0
      ELSEIF(.NOT.PARDBL(PAR(K+1:),SECS)) THEN
C
C NOT A REAL NUMBER.
C
        RETURN
      ENDIF
C
C CHECK THE SIGN AND MAGNITUDE OF SECS.
C
      IF(SECS.LT.0.0.OR.SECS.GE.60.0) RETURN
C
C NOW ATTEMPT TO READ THE DM/HM IN STR AS INTEGERS, EXIT TO 1002 IF THE
C READ FAILS.
C
      READ(STR,800,ERR=1002) I,J
C
C TEST THE DM/HM IN I AND J.
C
      IF(J.LT.0.OR.J.GE.60) RETURN
      IF(I.LT.0.OR.I.GT.90.OR.I.EQ.90.AND.(J.NE.0.OR.SECS.NE.0.0))
     &RETURN
C
C STRING IS A POSITION, RETURN AS SECONDS.
C
      POS=SIGN(DBLE(I*3600+J*60)+SECS,DBLE(SGN))
      PARPOS=.TRUE.
 1002 RETURN
C
C FORMAT STATEMENT.
C
  800 FORMAT(BN,2(I2:/))
C
C END OF LOGICAL FUNCTION PARPOS.
C
      END
