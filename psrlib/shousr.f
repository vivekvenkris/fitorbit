*DECK SHOUSR
C
C
C ***************************************************************
      SUBROUTINE SHOUSR ( LU,PARS,NPAR,IPAR,SHOEXT,N,IFAIL )
C ***************************************************************
C
C OUTPUTS USING A USER SUPPLIED SUBROUTINE SHOEXT, WHICH PRODUCES
C     AN OUTPUT FOR ELEMENT I OF A LIST OF N ELEMENTS
C     SHOEXT IS DEFINED AS :
C               SUBROUTINE SHOEXT ( LU, I, NLINES )
C     WHERE LU IS THE LOGICAL UNIT NUMBER
C           I IS THE LIST ELEMENT TO BE OUTPUT
C           NLINES RETURNS NUMBER OF LINES OUTPUT
C     A CALL IS MADE AT THE START OF THE LIST WITH I NEGATIVE
C     IN ORDER TO OUTPUT A TITLE OR HEADER
C THE RANGE OF I IS OBTAINED FROM THE COMMAND PARAMETERS FROM PARS(IPAR)
C TO PARS(NPAR)
C THE COMMANDS ARE INTERPRETED BY PARRNG AND MAY BE
C        FROM ... AND/OR TO ...
C OR ABBREVIATIONS THEREOF
C IF THE START(END) IS MISSING 1(N) IS ASSUMED
C
C IF THE OUTPUT IS TO A TERMINAL THE ROUTINE PAUSES FOR USER RESPONSE
C     AFTER EACH PAGE.
C
      INCLUDE 'PSRLIB.DEF'
      CHARACTER PARS(*)*(*),PRMTCC*1
      EXTERNAL SHOEXT
C
      PARAMETER ( NGROUP=5 )
      LOGICAL terminl
      DOUBLE PRECISION FROM,TO
      CHARACTER ANS*80
C
C     IF THERE IS NOTHING IN THE LIST, INFORM AND SKIP THE REST OF
C     THE COMMAND LINE.
C
      IF ( N.LE.0 ) THEN
         CALL OUTPUT ( LUMON,' No entries to be shown' )
         IF ( NPAR.GT.0 ) IPAR = NPAR+1
         RETURN
      ENDIF
C
C     PARRNG DECODES THE START AND FINISH ELEMENTS.
C
      CALL PARRNG (PARS,NPAR,IPAR,FROM,TO,FDATE,TDATE,.TRUE.,.FALSE.
     &            ,-1.0,IFAIL)
      IF ( IFAIL.NE.0 ) RETURN
C
C     CHECK THEM AGAINST THE ARRAY SIZE AND ENSURE THE NUMBER OF
C     ELEMENTS IS POSITIVE.
C
      IST = MIN(N,MAX(1,INT(MAX(DBLE(-MAXINT()),FROM))))
      IFIN= MIN(N,MAX(1,INT(MIN(DBLE(MAXINT()),TO))))
      NOUT = MAX(1,IFIN-IST+1)
      IFIN = IST+NOUT-1
C
C     NUMBER OF LINES OUTPUT AND NUMBER OF LINES PER PAGE.
C
      NLINES = 0
      NPPAGE = ABS(LUPAGL(LU))
C
C     CALL USER FUNCTION TO OUTPUT HEADER.
C
      CALL SHOEXT (LU,-1,NL)
      NLINES = NLINES+NL
C
C     MAIN OUTPUT LOOP.
C
      DO 10 I=1,NOUT
C
C        OUTPUT ENTRY I.
C
         CALL SHOEXT (LU,I+IST-1,NL)
         NLINES = NLINES+NL
C
C        IF THE TOTAL OUTPUT IS LARGER THAN A PAGE FULL,
C        THEN ENSURE THAT THERE IS SUFFICIENT ROOM FOR EACH 
C        GROUP OF LINES BY DOING A CONDITIONAL PAGE THROW.
C        IF A PAGE HAS NOT BEEN THROWN, DO A NEWLINE.
C
         IF ( NOUT.GT.NPPAGE.AND.MOD(I,NGROUP).EQ.0 ) THEN
            CALL PAGE (LU,NGROUP)
            IF ( LUREC(LU).NE.0 )THEN
              CALL OUTPUT (LU,' ')
              NLINES=NLINES+1
            ENDIF
         ENDIF
C
C        IF WRITING TO A TERMINAL, PAUSE FOR
C        USER INTERVENTION AFTER EACH PAGE EXCEPT THE LAST.
C
         IF ( terminl(LU,' ',IPAG,IWID).AND.NLINES.GE.NPPAGE-2
     &                                          .AND.I.NE.NOUT ) THEN
            CALL OUTPUT (LU,PRMTCC()//
     &         'Press return to continue, any key to exit : ')
            READ (*,999,ERR=2000,END=2000) ANS
            IF(ANS.NE.' ') GOTO 2000
            NLINES = 0
         ENDIF
   10 CONTINUE
 999  FORMAT (A80)
C
C     LABEL 2000 IS THE LOOP EXIT POINT.
C
 2000 CONTINUE
      CALL OUTPUT (LU,' ')
      RETURN
C
C END OF SUBROUTINE SHOUSR.
C
      END
